plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'
description = 'stay-stylish'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

configurations.all {
    resolutionStrategy {
        force "io.swagger.core.v3:swagger-core-jakarta:2.2.30"
        force "io.swagger.core.v3:swagger-models-jakarta:2.2.30"
        force "io.swagger.core.v3:swagger-annotations-jakarta:2.2.30"
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

// 의존성 버전 관리 (BOM)
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.3"
        mavenBom "org.springframework.ai:spring-ai-bom:1.0.0"
        mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:3.4.0"
    }
}

dependencies {
    // == Spring Boot Starters  ==
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // == 데이터베이스 & 영속성 ==
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core' // DB 스키마 마이그레이션 툴
    implementation 'org.flywaydb:flyway-database-postgresql' // Flyway의 PostgreSQL 지원
    runtimeOnly 'org.postgresql:postgresql' // PostgreSQL JDBC 드라이버

    // == Spring Security & OAuth2 & JWT ==
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client' // OAuth2 클라이언트 (소셜 로그인)
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // == 캐싱 (Redis & Caffeine) ==
    implementation 'org.springframework.boot:spring-boot-starter-data-redis' // Redis (캐시, Refresh Token 저장 등)
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8' // 고성능 로컬(인메모리) 캐시

    // == Spring AI (OpenAI) ==
    implementation 'org.springframework.ai:spring-ai-starter-model-openai' // Spring AI OpenAI 연동

    // == 모니터링 & 내결함성 ==
    implementation 'org.springframework.boot:spring-boot-starter-actuator' // 애플리케이션 상태 모니터링
    implementation 'io.micrometer:micrometer-registry-prometheus' // Prometheus 메트릭 수집
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j' // 서킷 브레이커
    implementation 'org.springframework.boot:spring-boot-starter-aop' // Resilience4j 등 AOP 기반 기능 지원

    // == API 문서화 ==
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8' // Swagger UI
    implementation "io.swagger.core.v3:swagger-annotations-jakarta:2.2.30" // Swagger 어노테이션

    // == AWS ==
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store'

    // == 이메일 & 템플릿 ==
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

    // == 유틸리티 ==
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'me.paulschwarz:spring-dotenv:3.0.0' // .env 파일 로드
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml' // XML 파싱 (기상청 API 응답 처리용)
    implementation 'org.apache.commons:commons-lang3:3.18.0' // Java 기본 유틸리티 확장

    // == 테스트 ==
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.jetbrains:annotations:24.1.0'


    // 메트릭 수집용 (프로메테우스, 그라파나)
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.micrometer:micrometer-core'




}

tasks.named('test') {
    useJUnitPlatform()
}
